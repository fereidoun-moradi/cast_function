
//Tank1 Initial Status
env boolean tank1_low = true;
env boolean tank1_medium = false;
env boolean tank1_high = false;
//----------------------------------------------------
//Tank2 Initial Status
env boolean tank2_low = true;
env boolean tank2_medium = false;
env boolean tank2_high = false;
//----------------------------------------------------
//Tank3 Initial Status
env boolean tank3_low = true;
env boolean tank3_high = false;
//----------------------------------------------------
// Sense interval for each sensor
env int timingInterval = 1;

reactiveclass PLC1(10){
	knownrebecs{
		Pump1 pump1;
		Valve valve;
	}
	statevars{
		boolean openReqPlc2;
		boolean pump1On;
		boolean valveOpen;
		int sensedTank1WaterLevel;
	}
	PLC1(){	 
		openReqPlc2 = false;
		pump1On = false;
		valveOpen = false;
		sensedTank1WaterLevel = 1;
	}
	@priority(3)
	msgsrv plc1_getSense(int waterLevel){
		sensedTank1WaterLevel = waterLevel;
		if (waterLevel == 1 && pump1On == false) { 
			pump1.pump1_on();
			pump1On = true;
		} else if (waterLevel == 2) {
			if(!valveOpen  && openReqPlc2) {
				valve.valve_open();
				valveOpen = true;
			} else if(valveOpen  && openReqPlc2) {
				valve.valve_keepOnWaterFlow();
			}
			if(!pump1On){
				pump1.pump1_on();
				pump1On = true;
			} else {
				pump1.pump1_keepOnpumping();
			}
		} else if (waterLevel == 3) { 
			if(!valveOpen  && openReqPlc2 == true) {
				valve.valve_open();
				valveOpen = true;
			} else if(valveOpen && openReqPlc2 == true) {
				valve.valve_keepOnWaterFlow();
			} if(pump1On) {
				pump1.pump1_off();
				pump1On = false;
			} else if(!openReqPlc2) {
				valve.valve_close();
				valveOpen = false;
			}
		} 
	}
	@priority(2)
	msgsrv plc1_openReq(){
		openReqPlc2 = true;
	}
	@priority(1)
	msgsrv plc1_closeReq(){
		openReqPlc2 = false;
		valve.valve_close();
		valveOpen = false;
	}

}



reactiveclass PLC2(10){
	knownrebecs{
		PLC1 plc1;
		PLC3 plc3;
	}
	statevars{
		int previous_waterLevel;
		int sensedTank2WaterLevel;
	}
	PLC2(){	 
		previous_waterLevel = 1;
		sensedTank2WaterLevel = 1;
	}
	msgsrv plc2_getSense(int waterLevel){
		sensedTank2WaterLevel = waterLevel;
		if (waterLevel == 1) { 
			plc1.plc1_openReq();
		} else if (previous_waterLevel == 2 && waterLevel == 2) { 
			plc1.plc1_closeReq();
			plc3.plc3_onReq();			
		} else if (previous_waterLevel == 3 && waterLevel == 2) { 
			plc1.plc1_openReq();			
		} 
		previous_waterLevel = waterLevel;
	}
}

reactiveclass PLC3(10){
	knownrebecs {
		Pump2 pump2;
	}
	statevars{
		boolean onReqPlc2;
		int sensedTank3WaterLevel;
	}
	PLC3(){	 
		onReqPlc2 = false;
		sensedTank3WaterLevel = 1;
	}
	@priority(2)
	msgsrv plc3_getSense(int waterLevel){
		sensedTank3WaterLevel = waterLevel;
		if (waterLevel == 3) {
			if(onReqPlc2 == true){
				pump2.pump2_on();
				onReqPlc2 == false;
			} else {
				pump2.pump2_off();
			}
		} else if(waterLevel == 1) {
			pump2.pump2_off();
		}
	}
	@priority(1)
	msgsrv plc3_onReq(){
		onReqPlc2 = true;
	}
}


reactiveclass Tank1(10){
	knownrebecs{
		sensorTank1 sensor;
		Tank2 tank2;
		Valve valve;	
	}
	statevars{
		boolean underFlow,low,medium,high,overFlow;
	}
	Tank1(){
		underFlow = false;
		low = tank1_low; 
		medium = tank1_medium;
		high = tank1_high;
		overFlow = false;
	}
	msgsrv tank1_status(){
		// status: 0 to 4, respectively show underFlow,low,medium,high, and overFlow
		if (underFlow){
		sensor.sensor1_reportStatus(0); 
		} else if (low){
		sensor.sensor1_reportStatus(1);
		} else if (medium){
		sensor.sensor1_reportStatus(2);
		} else if (high){
		sensor.sensor1_reportStatus(3);
		} else if (overFlow){
		sensor.sensor1_reportStatus(4);
		}
	}
	@priority(1)
	msgsrv tank1_waterIncrease(){	
		if (low) { 
			low = false;
			medium = true;
		} else if (medium) { 
			medium = false;
			high = true;
		} else if (high) { 
			high = false;
			overFlow = true;		
		}
		self.tank1_status();
	}
	@priority(2)
	msgsrv tank1_waterDecrease(){	
		if (high) { 
			high = false;
			medium = true;
		} else if (medium) {
			medium = true;
			low = false;
			tank2.tank2_waterIncrease();

		} else if (low) { 
			low = false;
			underFlow = true;
		}
		self.tank1_status();
	}
}


reactiveclass Tank2(15){
	knownrebecs{
		sensorTank2 sensor;
		Tank3 tank3;
		Tank1 tank1;
		reverseUnit unit;
	}
	statevars{
		boolean underFlow,low,medium,high,overFlow;
	}
	Tank2(){
		underFlow = false;
		low = tank2_low; 
		medium = tank2_medium;
		high = tank2_high;
		overFlow = false;
	}
	msgsrv tank2_status(){	
		if (underFlow){
			sensor.sensor2_reportStatus(0); 
		}else if (low){
			sensor.sensor2_reportStatus(1);
		}else if (medium){
			sensor.sensor2_reportStatus(2);
		}else if (high){
			sensor.sensor2_reportStatus(3);
		}else if (overFlow){
			sensor.sensor2_reportStatus(4);
		}
	}

	msgsrv tank2_waterIncrease(){	
		if (low) { 
			low = false;
			medium = true;
		} else if (medium) { 
			if(sender == tank1) {
				medium = true;
				high = false;
				tank3.tank3_waterIncrease();
			} else {
				medium = false;
				high = true;
				// clean water flows out when tank2 water-level reaches high
				unit.cleanWater(); 
				tank1.tank1_status() after(timingInterval);				
			}
			
		} else if (high) { 
			high = false;
			overFlow = true;
		}
		self.tank2_status();
	}
	msgsrv tank2_waterDecrease(){	
		if (high) { 
			high = false;
			medium = true;
		} else if (medium) { 
			medium = false;
			low = true;
		} else if (low) { 
			low = false;
			underFlow = true;
		}
		self.tank2_status();
	}

}

reactiveclass Tank3(10){
	knownrebecs{
		sensorTank3 sensor;
		Tank2 tank2;
	}
	statevars{
		boolean underFlow,low,high,overFlow;
	}
	Tank3(){
		underFlow = false;
		low = tank3_low;
		high = tank3_high;
		overFlow = false;
	}
	msgsrv tank3_status(){	
		if (underFlow){
		sensor.sensor3_reportStatus(0); 
		}else if (low){
		sensor.sensor3_reportStatus(1);
		}else if (high){
		sensor.sensor3_reportStatus(3);
		}else if (overFlow){
		sensor.sensor3_reportStatus(4);
		}
	}
	msgsrv tank3_waterIncrease(){	
		if (low) { 
			low = false;
			high = true;
		} else if (high) { 
			high = false;
			overFlow = true;
		}
		self.tank3_status();
		
	}
	msgsrv tank3_waterDecrease(){	
		if (high) {
			high = false;
			low = true;
			tank2.tank2_waterIncrease();
		} else if (low) { 
			low = false;
			underFlow = true;
		}
		self.tank3_status();
		
	}
}


reactiveclass sensorTank1(10){
	knownrebecs{
		Tank1	tank1;
		PLC1	plc1;
	}
	sensorTank1(){ 
		self.sensor1_sense();	
	}
	msgsrv sensor1_sense(){	
		tank1.tank1_status();	
	}
	msgsrv sensor1_reportStatus(int waterLevel) {	
		plc1.plc1_getSense(waterLevel) after(timingInterval);
	}
}

reactiveclass sensorTank2(10){
	knownrebecs{
		Tank2	tank2;
		PLC2	plc2;
	}
	sensorTank2(){ 
		self.sensor2_sense();	
	}
	msgsrv sensor2_sense(){	
		tank2.tank2_status();	
	}
	msgsrv sensor2_reportStatus(int waterLevel) {	
		plc2.plc2_getSense(waterLevel) after(timingInterval);
	}

}

reactiveclass sensorTank3(10){
	knownrebecs{
		Tank3	tank3;
		PLC3	plc3;
	}
	sensorTank3(){ 
		self.sensor3_sense();	
	}
	msgsrv sensor3_sense(){	
		tank3.tank3_status();	
	}
	msgsrv sensor3_reportStatus(int waterLevel) {	
		plc3.plc3_getSense(waterLevel) after(timingInterval);
	}

}


reactiveclass Pump1(10){
	knownrebecs{
		Tank1 tank1;
	}
	statevars{
		boolean pump1_on;
	}
	Pump1(){
	 	pump1_on = false;
	}
	msgsrv pump1_on(){	
		pump1_on = true;
		tank1.tank1_waterIncrease(); 
	}
	msgsrv pump1_keepOnpumping(){
		pump1_on = true;
	}
	msgsrv pump1_off(){
		pump1_on = false;	
	}

}

reactiveclass Valve(10){
	knownrebecs{
		Tank1 tank1;
	}
	statevars{
		boolean valve_open;
	}
	Valve(){
	 	valve_open = false;	 	
	}
	msgsrv valve_open(){	
		valve_open = true;
		tank1.tank1_waterDecrease();
	}
	msgsrv valve_keepOnWaterFlow(){
		valve_open = true;
		tank1.tank1_waterDecrease();
	}
	msgsrv valve_close(){
		valve_open = false;
		tank1.tank1_status();	
	}

}

reactiveclass Pump2(10){
	knownrebecs{
		Tank3 tank3;
	}
	statevars{
		boolean pump2_on;
	}
	Pump2(){
	 	pump2_on = false;
	}
	msgsrv pump2_on(){	
		pump2_on = true;
		tank3.tank3_waterDecrease(); 
	}
	msgsrv pump2_off(){
		pump2_on = false;	
	}

}


reactiveclass reverseUnit(5){
	knownrebecs{
		Tank2 tank2;
	}
	statevars {
		boolean drinkableWater;
	}
	reverseUnit(){
		drinkableWater = false;
	}
	msgsrv cleanWater(){
		drinkableWater = true;
		tank2.tank2_waterDecrease() after(timingInterval);
	}
}


main{
	@priority(2) PLC1 plc1(pump1, valve):();				
	@priority(1) PLC2 plc2(plc1, plc3):();
	PLC3 plc3(pump2):();
	
	//@priority(2)
	Pump1 pump1(tank1):();
	Pump2 pump2(tank3):();
	//@priority(1)
	Valve valve(tank1):();						
	reverseUnit unit(tank2):();
	
	Tank1 tank1(sensor1, tank2, valve):();				
	Tank2 tank2(sensor2, tank3, tank1, unit):();
	Tank3 tank3(sensor3, tank2):();
	
	sensorTank1 sensor1(tank1, plc1):();
	sensorTank2 sensor2(tank2, plc2):();	
	sensorTank3 sensor3(tank3, plc3):();
}
